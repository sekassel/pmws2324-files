plugins {
    id 'java'
    id 'application'

    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id 'org.openjfx.javafxplugin' version '0.0.12'

    // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
    id 'org.fulib.fulibGradle' version '0.5.0'
}

group 'de.uniks.pmws2324.tiny'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    // https://mvnrepository.com/artifact/fr.brouillard.oss/cssfx
    implementation group: 'fr.brouillard.oss', name: 'cssfx', version: '11.5.1'
    implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'

    // https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-jackson
    implementation group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.9.0'

    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'

    // --------------- Fulib ---------------

    // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
    fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.7.0'

    // optional, to override the version of fulib used by fulibScenarios:
    // https://mvnrepository.com/artifact/org.fulib/fulib
    fulibScenarios group: 'org.fulib', name: 'fulib', version: '1.6.1'

    // --------------- Testing ---------------

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.16-alpha'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: 'jdk-12.0.1+2'
}

javafx {
    version = '17.0.8'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.swing']
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        showStackTraces = true
    }

    systemProperties = [
            'java.awt.headless': 'true',
            'testfx.robot'     : 'glass',
            'testfx.headless'  : 'true',
            'glass.platform'   : 'Monocle',
            'monocle.platform' : 'Headless',
            'prism.order'      : 'sw',
            'prism.text'       : 't2k',
    ]

    jvmArgs += [
            '-Xmx2g',
            '--add-opens', 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
            '--add-opens', 'javafx.graphics/com.sun.javafx.application=ALL-UNNAMED',
    ]
}
